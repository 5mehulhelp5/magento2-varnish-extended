varnishtest "Force cache refresh for whitelisted IP addresses"

barrier b1 cond 2

server s1 {
    # first request will be the probe, handle it and be on our way
    rxreq
    expect req.url == "/health_check.php"
    txresp

    # the probe expects the connection to close
    close
    barrier b1 sync
    accept

    rxreq
    expect req.url == "/"
    expect req.method == "GET"
    txresp

    rxreq
    expect req.url == "/"
    expect req.method == "GET"
    txresp

    rxreq
    expect req.url == "/"
    expect req.method == "GET"
    txresp
} -start

# Generate the VCL file based on included variables and write it to output.vcl
shell {
    export s1_addr="${s1_addr}"
    export s1_port="${s1_port}"
    ${testdir}/helpers/parse_vcl.pl "${testdir}/../../etc/varnish6.vcl" "${tmpdir}/output.vcl"
}

varnish v1 -arg "-f" -arg "${tmpdir}/output.vcl" -arg "-p" -arg "vsl_mask=+Hash" -start

# make sure the probe request fired
barrier b1 sync

client c1 {
    txreq -method "GET" -url "/"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS"

    txreq -method "GET" -url "/"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "HIT"

    txreq -method "GET" -url "/" -hdr "pragma:no-cache"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS-FORCED"

    txreq -method "GET" -url "/"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "HIT"

    txreq -method "GET" -url "/" -hdr "cache-control:no-cache"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS-FORCED"

    txreq -method "GET" -url "/"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "HIT"
} -run