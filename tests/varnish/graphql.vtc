varnishtest "GraphQL X-Magento-Cache-Id validation & cache bypassing"

server s1 {
    # first request will be the probe, handle it and be on our way
    rxreq
    expect req.url == "/health_check.php"
    txresp

    # the probe expects the connection to close
    close
    accept

    rxreq
    expect req.url == "/graphql"
    expect req.method == "GET"
    txresp

    rxreq
    expect req.url == "/graphql"
    expect req.method == "GET"
    txresp

    rxreq
    expect req.url == "/graphql"
    expect req.method == "GET"
    txresp -hdr "X-Magento-Cache-Id: 12345"

    rxreq
    expect req.url == "/graphql"
    expect req.method == "GET"
    txresp

    rxreq
    expect req.url == "/graphql"
    expect req.method == "GET"
    txresp -hdr "X-Magento-Cache-Id: 12345"

} -start

# Generate the VCL file based on included variables and write it to output.vcl
shell {
    export s1_addr="${s1_addr}"
    export s1_port="${s1_port}"
    ${testdir}/helpers/parse_vcl.pl "${testdir}/../../etc/varnish6.vcl" "${tmpdir}/output.vcl"
}

varnish v1 -arg "-f" -arg "${tmpdir}/output.vcl" -arg "-p" -arg "vsl_mask=+Hash" -start

# make sure the probe request fired
delay 1

client c1 {
    txreq -method "GET" -url "/graphql"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS"

    txreq -method "GET" -url "/graphql"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "HIT"

    # If backend does not match the X-Magento-Cache-Id, it should be uncacheable
    txreq -method "GET" -url "/graphql" \
        -hdr "X-Magento-Cache-Id: 1234"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "UNCACHEABLE"

    txreq -method "GET" -url "/graphql" \
        -hdr "X-Magento-Cache-Id: 12345"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS"

    txreq -method "GET" -url "/graphql" \
        -hdr "Authorization: Bearer 12345"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "UNCACHEABLE"

    txreq -method "GET" -url "/graphql" \
        -hdr "Authorization: Bearer 12345" \
        -hdr "X-Magento-Cache-Id: 12345"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS"
} -run