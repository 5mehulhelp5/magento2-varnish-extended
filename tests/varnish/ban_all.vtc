varnishtest "ban: purge a tag"

barrier b1 cond 2

server s1 {
    # first request will be the probe, handle it and be on our way
    rxreq
    expect req.url == "/health_check.php"
    txresp

    # the probe expects the connection to close
    close
    barrier b1 sync
    accept

    # Fill the cache
    rxreq
    expect req.url == "/"
    expect req.method == "GET"
    txresp -hdr "X-Magento-Tags: cat_c,cat_c_38,cat_p_694,cat_c_20" -hdr "Context: fill the cache"

    rxreq
    expect req.url == "/hero-hoodie.html"
    expect req.method == "GET"
    txresp -hdr "X-Magento-Tags: cat_c,cat_c_37,cat_p_694,cat_c_21" -hdr "Context: fill the cache"

    rxreq
    expect req.url == "/breathe-easy-tank.html"
    expect req.method == "GET"
    txresp -hdr "X-Magento-Tags: cat_c,cat_c_37,cat_p_695,cat_c_21" -hdr "Context: fill the cache"

    # ban all
    rxreq
    expect req.url == "/"
    expect req.method == "GET"
    txresp -hdr "X-Magento-Tags: cat_c,cat_c_38,cat_p_694,cat_c_20" -hdr "Context: ban all"

    rxreq
    expect req.url == "/hero-hoodie.html"
    expect req.method == "GET"
    txresp -hdr "X-Magento-Tags: cat_c,cat_c_37,cat_p_694,cat_c_21"  -hdr "Context: ban all"

    rxreq
    expect req.url == "/breathe-easy-tank.html"
    expect req.method == "GET"
    txresp -hdr "X-Magento-Tags: cat_c,cat_c_37,cat_p_695,cat_c_21" -hdr "Context: ban all"
} -start

# Generate the VCL file based on included variables and write it to output.vcl
shell {
    export s1_addr="${s1_addr}"
    export s1_port="${s1_port}"
    export USE_XKEY_VMOD=0
    ${testdir}/helpers/parse_vcl.pl "${testdir}/../../etc/varnish6.vcl" "${tmpdir}/output.vcl"
}

varnish v1 -arg "-f" -arg "${tmpdir}/output.vcl" -arg "-p" -arg "vsl_mask=+Hash"  -arg "-p" -arg "ban_lurker_age=3" -start

# make sure the probe request fired
barrier b1 sync

client c1 {
    # filling the cache
    txreq -method "GET" -url "/" -hdr "Context: fill the cache"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS"

    txreq -method "GET" -url "/hero-hoodie.html" -hdr "Context: fill the cache"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS"

    txreq -method "GET" -url "/breathe-easy-tank.html" -hdr "Context: fill the cache"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS"

    # ban that affects all objects
    txreq -method "PURGE" -url "/" -hdr "X-Magento-Tags-Pattern: .*"
    rxresp
    expect resp.status == 200
    expect resp.http.Content-Type == "application/json"
    expect resp.body == "{ \"invalidated\": 0 }"
    expect resp.reason == "OK"
} -run

# Checking the ban list
varnish v1 -cliexpect "obj.http.X-Magento-Tags \\~ \\.\\*" ban.list

client c1 {
    # ban cause cache misses
    txreq -method "GET" -url "/" -hdr "Context: ban all"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS"

    txreq -method "GET" -url "/hero-hoodie.html" -hdr "Context: ban all"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS"

    txreq -method "GET" -url "/breathe-easy-tank.html" -hdr "Context: ban all"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS"
} -run