varnishtest "xkey: soft purge a tag"

server s1 {
    # first request will be the probe, handle it and be on our way
    rxreq
    expect req.url == "/health_check.php"
    txresp

    # the probe expects the connection to close
    close
    accept

    # Fill the cache
    rxreq
    expect req.url == "/"
    expect req.method == "GET"
    txresp -hdr "X-Magento-Tags: cat_c,cat_c_38,cat_p_694,cat_c_20" -hdr "Context: fill the cache"

    rxreq
    expect req.url == "/hero-hoodie.html"
    expect req.method == "GET"
    txresp -hdr "X-Magento-Tags: cat_c,cat_c_37,cat_p_694,cat_c_21" -hdr "Context: fill the cache"

    rxreq
    expect req.url == "/breathe-easy-tank.html"
    expect req.method == "GET"
    txresp -hdr "X-Magento-Tags: cat_c,cat_c_37,cat_p_695,cat_c_21" -hdr "Context: fill the cache"

    # soft purge cat_p_694
    rxreq
    expect req.url == "/"
    expect req.method == "GET"
    txresp -hdr "X-Magento-Tags: cat_c,cat_c_38,cat_p_694,cat_c_20" -hdr "Context: soft purge cat_p_694"

    rxreq
    expect req.url == "/hero-hoodie.html"
    expect req.method == "GET"
    txresp -hdr "X-Magento-Tags: cat_c,cat_c_37,cat_p_694,cat_c_21" -hdr "Context: soft purge cat_p_694"
} -start

# Generate the VCL file based on included variables and write it to output.vcl
shell {
    export s1_addr="${s1_addr}"
    export s1_port="${s1_port}"
    ${testdir}/helpers/parse_vcl.pl "${testdir}/../../etc/varnish6.vcl" "${tmpdir}/output.vcl"
}

varnish v1 -arg "-f" -arg "${tmpdir}/output.vcl" -arg "-p" -arg "vsl_mask=+Hash" -start

# make sure the probe request fired
delay 1

client c1 {
    # filling the cache
    txreq -method "GET" -url "/" -hdr "Context: fill the cache"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS"

    txreq -method "GET" -url "/hero-hoodie.html" -hdr "Context: fill the cache"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS"

    txreq -method "GET" -url "/breathe-easy-tank.html" -hdr "Context: fill the cache"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS"

    # soft purge that affects "/" & "/hero-hoodie.html"
    txreq -method "PURGE" -url "/" -hdr "X-Magento-Tags-Pattern: ((^|,)cat_p_694(,|$))" -hdr "X-Magento-Purge-Soft: 1"
    rxresp
    expect resp.status == 200
    expect resp.http.Content-Type == "application/json"
    expect resp.body == "{ \"invalidated\": 2 }"
    expect resp.reason == "OK"

    # soft purge causes "HIT-GRACE"
    txreq -method "GET" -url "/" -hdr "Context: soft purge cat_p_694"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "HIT-GRACE"

    txreq -method "GET" -url "/hero-hoodie.html" -hdr "Context: soft purge cat_p_694"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "HIT-GRACE"

    # "/breathe-easy-tank.html" is unaffected
    txreq -method "GET" -url "/breathe-easy-tank.html" -hdr "Context: soft purge cat_p_694"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "HIT"
} -run