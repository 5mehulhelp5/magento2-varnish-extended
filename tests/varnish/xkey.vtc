varnishtest "xkey"

server s1 {
    # first request will be the probe, handle it and be on our way
    rxreq
    expect req.url == "/health_check.php"
    txresp

    # the probe expects the connection to close
    close
    accept

    rxreq
    expect req.url == "/"
    expect req.method == "GET"
    txresp -hdr "X-Magento-Tags: cat_c,cat_c_38,cat_p_694,cat_c_20"

    rxreq
    expect req.url == "/hero-hoodie.html"
    expect req.method == "GET"
    txresp -hdr "X-Magento-Tags: cat_c,cat_c_37,cat_p_694,cat_c_21"

    rxreq
    expect req.url == "/breathe-easy-tank.html"
    expect req.method == "GET"
    txresp -hdr "X-Magento-Tags: cat_c,cat_c_37,cat_p_695,cat_c_21"

    rxreq
    expect req.url == "/"
    expect req.method == "GET"
    txresp -hdr "X-Magento-Tags: cat_c,cat_c_38,cat_p_694,cat_c_20"

    rxreq
    expect req.url == "/hero-hoodie.html"
    expect req.method == "GET"
    txresp -hdr "X-Magento-Tags: cat_c,cat_c_37,cat_p_694,cat_c_21"
} -start

# generate usable VCL pointing towards s1
# mostly, we replace the place-holders, but we also jack up the probe
# interval to avoid further interference
shell {
    # Backend configuration
    export HOST="${s1_addr}"
    export PORT="${s1_port}"
    export GRACE_PERIOD="300"

    # SSL configuration
    export SSL_OFFLOADED_HEADER="X-Forwarded-Proto"

    # Feature flags
    export USE_XKEY_VMOD="1"
    export ENABLE_MEDIA_CACHE="1"
    export ENABLE_STATIC_CACHE="1"

    # ACL list with IPs
    export ACCESS_LIST="server1 server2"
    export SERVER1_IP="${s1_addr}"
    export SERVER2_IP="10.0.0.1"

    # Cookie list with regex patterns
    export PASS_ON_COOKIE_PRESENCE="cookie1 cookie2"
    export COOKIE1_REGEX="^ADMIN"
    export COOKIE2_REGEX="^PHPSESSID"

    # Performance parameters
    export TRACKING_PARAMETERS="utm_source|utm_medium|utm_campaign|gclid|cx|ie|cof|siteurl"

    # Design exceptions
    export DESIGN_EXCEPTIONS_CODE='if (req.url ~ "^/media/theme/") { hash_data("design1"); }'

    ${testdir}/helpers/parse_vcl.pl "${testdir}/../../etc/varnish6.vcl" "${tmpdir}/output.vcl"
}

varnish v1 -arg "-f" -arg "${tmpdir}/output.vcl" -arg "-p" -arg "vsl_mask=+Hash" -start

# make sure the probe request fired
delay 1

client c1 {
    txreq -method "GET" -url "/"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS"

    txreq -method "GET" -url "/hero-hoodie.html"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS"

    txreq -method "GET" -url "/breathe-easy-tank.html"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS"

    txreq -method "GET" -url "/"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "HIT"

    txreq -method "GET" -url "/hero-hoodie.html"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "HIT"

    txreq -method "GET" -url "/breathe-easy-tank.html"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "HIT"

    txreq -method "PURGE" -url "/" -hdr "X-Magento-Tags-Pattern: ((^|,)cat_p_694(,|$))" -hdr "X-Magento-Purge-Soft: 1"
    rxresp
    expect resp.status == 200
    expect resp.http.Content-Type == "application/json"
    expect resp.body == "{ \"invalidated\": 2 }"
    expect resp.reason == "OK"

    txreq -method "GET" -url "/"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "HIT-GRACE"

    txreq -method "GET" -url "/hero-hoodie.html"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "HIT-GRACE"

    txreq -method "GET" -url "/breathe-easy-tank.html"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "HIT"
} -run